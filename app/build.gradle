plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'inc.cbrt4.flexiblepagecontroldemo'
    compileSdkVersion 33

    defaultConfig {
        applicationId defaultApplicationId
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    lint {
        lintConfig file('lint.xml')
    }

    buildFeatures {
        viewBinding = true
    }

    flavorDimensions "product"

    productFlavors {
        color {
            dimension "product"
            applicationId "${defaultApplicationId}.color"
            buildConfigField "boolean", "IS_COLOR", "true"
        }

        picture {
            dimension "product"
            applicationId "${defaultApplicationId}.picture"
            buildConfigField "boolean", "IS_COLOR", "false"
        }

    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "flexiblePageControlDemo-${productFlavors.name}-${defaultConfig.versionName}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "androidx.appcompat:appcompat:$androidxVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    releaseApi project(path: ':flexiblepageindicator')
    debugApi project(path: ':flexiblepageindicator')
}
